CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // if (zx == 1) set x = 0
    Mux16(a=x, b=false, sel=zx, out=x1);
    // if (nx == 1) set x = !x
    Not16(in=x1, out=notx1);
    Mux16(a=x1, b=notx1, sel=nx, out=x2);

    // if (zy == 1) set y = 0
    Mux16(a=y, b=false, sel=zy, out=y1);
    // if (ny == 1) set y = !y
    Not16(in=y1, out=noty1);
    Mux16(a=y1, b=noty1, sel=ny, out=y2);

    // if (f == 1) out = x + y, else out = x & y
    Add16(a=x2, b=y2, out=addXY);
    And16(a=x2, b=y2, out=andXY);
    Mux16(a=andXY, b=addXY, sel=f, out=fOut);

    // if (no == 1) out = !out
    Not16(in=fOut, out=notFOut);
    Mux16(a=fOut, b=notFOut, sel=no, out=out, out[15]=ng, out[0..7]=lowByte, out[8..15]=highByte);

    Or8Way(in=lowByte, out=orLow);
    Or8Way(in=highByte, out=orHigh);
    Or(a=orLow, b=orHigh, out=notZr);
    Not(in=notZr, out=zr);
}